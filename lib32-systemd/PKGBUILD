pkgname=lib32-systemd
_pkgbasename=systemd
# latest commit on stable branch
_commit='9d34e79ae8ef891adf3757f9248566def70471ad'
# Bump this to latest major release for signed tag verification,
# the commit count is handled by pkgver() function.
pkgver=242.84
pkgrel=1
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="https://www.github.com/systemd/systemd"
license=('GPL2' 'LGPL2.1')
depends=('lib32-gcc-libs' 'lib32-libcap' 'lib32-libgcrypt' 'lib32-xz' "$_pkgbasename=$pkgver")
makedepends=('git' 'gperf' 'intltool' 'lib32-acl' 'lib32-bzip2'
             'lib32-curl' 'lib32-dbus' 'lib32-gcc-libs' 'lib32-glib2'
             'lib32-gnutls' 'lib32-libelf' 'lib32-libidn2' 'lib32-pcre2'
             'libxslt' 'meson')
options=('strip')
validpgpkeys=('63CDA1E5D3FC22B998D20DD6327F26951A015CC4'  # Lennart Poettering <lennart@poettering.net>
              '5C251B5FC54EB2F80F407AAAC54CA336CFEB557E') # Zbigniew JÄ™drzejewski-Szmek <zbyszek@in.waw.pl>
source=(# fragment is latest tag for source verification, final merge in prepare()
        "git+https://github.com/systemd/systemd-stable#tag=v${pkgver%.*}"
        "git+https://github.com/systemd/systemd#tag=v${pkgver%.*}")
sha256sums=('SKIP'
            'SKIP')

_backports=(
  # basic/socket-util: put a limit on the loop to flush connections
  '67962036f6c6cfd34828c1f1f1fbdc0018fb9898'

  # network: logs link state change
  '0beb9542e90ab1c5d1507a1046a326fbcf73861c'

  # network: drop invalid assertion
  '51aba17b88617515e037e8985d3a4ea871ac47fe'
  # network: fix assertion when link get carrier
  'b9ea3d2e47b3741f3f46ba9c5e19640136933b71'

  # network: do not use ordered_set_printf() for DOMAINS= or ROUTE_DOMAINS=
  'fe0e16db093a7da09fcb52a2bc7017197047443d'

  # network: honor MTUBytes= setting
  '933c70a0a4e4fac47d18e0348ae97ee3d48dc139'
  # network: bump MTU bytes only when MTUByte= is not set
  'f6fcc1c2a41eae749467de58453174296b635a69'

  # network: do not send ipv6 token to kernel
  '9f6e82e6eb3b6e73d66d00d1d6eee60691fb702f'

  # cgroup-util: kill also threads
  'e48fcfef06d81bf08607d3c1657fdc6aa1e9a6ee'
)


_reverts=(
)

prepare() {
  cd "$_pkgbasename-stable"

  # add upstream repository for cherry-picking
  git remote rm upstream || true
  git remote add -f upstream ../systemd
  # merge the latest stable commit (fast-foward only to make sure
  # the verified tag is in)
  git merge --ff-only "${_commit}"

  local c
  for c in "${_backports[@]}"; do
    git cherry-pick -n "$c"
  done
  for c in "${_reverts[@]}"; do
    git revert -n "$c"
  done
}

pkgver() {
  local version count

  cd "$_pkgbasename-stable"

  version="$(git describe --abbrev=0 --tags)"
  count="$(git rev-list --count ${version}..)"
  printf '%s.%s' "${version#v}" "${count}"
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  local _timeservers=({0..3}.chakra.pool.ntp.org)
  local _nameservers=(
    # We use these public name services, ordered by their
    # privacy policy (hopefully):
    #  * Cloudflare (https://1.1.1.1/)
    #  * Quad9 without filtering (https://www.quad9.net/)
    #  * Google (https://developers.google.com/speed/public-dns/)
    1.1.1.1
    9.9.9.10
    8.8.8.8
    2606:4700:4700::1111
    2620:fe::10
    2001:4860:4860::8888
  )

  local meson_options=(
    --libexecdir	/usr/lib32
    --libdir		/usr/lib32

    -Dversion-tag="${pkgver}-${pkgrel}-chakra"

    -Daudit=false
    -Dgnuefi=false
    -Dima=false
    -Dlibidn2=true
    -Dlz4=false
    -Dblkid=false
    -Dkmod=false
    -Dlibcryptsetup=false
    -Dlibiptc=false
    -Dmanpages=false
    -Dnetworkd=false
    -Dpam=false
    -Dpython=false
    -Dseccomp=false
    -Dman=false
    -Dhtml=false
    -Dmicrohttpd=false
    -Dqrencode=false
    -Dxkbcommon=false

    -Ddbuspolicydir=/usr/share/dbus-1/system.d
    -Ddefault-dnssec=no
    -Ddefault-hierarchy=hybrid
    -Ddefault-locale=C
    -Ddefault-kill-user-processes=false
    -Dfallback-hostname='chakralinux'
    -Dntp-servers="${_timeservers[*]}"
    -Ddns-servers="${_nameservers[*]}"
    -Drpmmacrosdir=no
    -Dsysvinit-path=
    -Dsysvrcnd-path=
  )

  chakra-meson "$_pkgbasename-stable" build "${meson_options[@]}"

  ninja -C build
}

check() {
  meson test -C build || true
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,sbin,include,lib,share}

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s systemd "$pkgdir/usr/share/licenses/lib32-systemd"
}
