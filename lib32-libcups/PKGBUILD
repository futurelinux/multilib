_pkgbasename=libcups
pkgname=lib32-$_pkgbasename
pkgver=2.2.8
pkgrel=1
pkgdesc="The CUPS Printing System - client libraries (32-bit)"
arch=('x86_64')
license=('GPL')
url="http://www.cups.org/"
depends=('lib32-krb5' 'lib32-libtiff' 'lib32-libpng' 'lib32-gnutls' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
source=(https://github.com/apple/cups/releases/download/v${pkgver}/cups-${pkgver}-source.tar.gz)
md5sums=('33150d08993a04c8e22176e93805a051')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/cups-${pkgver}

  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --disable-ldap --enable-raw-printing --disable-gssapi --disable-dbus \
     --enable-ssl=yes --enable-threads --enable-libusb=no \
     --disable-avahi --disable-dnssd \
     --with-optim="$CFLAGS" --libdir=/usr/lib32
  make libs
}

package() {
  cd ${srcdir}/cups-${pkgver}
  make BUILDROOT=${pkgdir} install-libs
  rm -rf "$pkgdir/usr/lib"
}
