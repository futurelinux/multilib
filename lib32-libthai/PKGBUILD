# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Chaiwat Suttipongsakul <cwt114@gmail.com>

_pkgbasename=libthai
pkgname=lib32-${_pkgbasename}
pkgver=0.1.22
pkgrel=1
pkgdesc='Thai language support routines'
url='http://linux.thai.net/projects/libthai'
arch=('x86_64')
license=('LGPL')
depends=('lib32-libdatrie' "$_pkgbasename=$pkgver")
makedepends=('gcc-multilib')
source=("http://linux.thai.net/pub/thailinux/software/libthai/libthai-${pkgver}.tar.xz")
sha256sums=('bdb291dcc3a6784e86b1b342ec75005e9ab487f8c1decc7c3e7d18d1bbc9b7b7')

build() {
  cd ${_pkgbasename}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}

# vim: ts=2 sw=2 et:
