
# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): AlmAck <AlmAck[at]chakraos[dot]org>

_pkgbasename=fontconfig
pkgname=lib32-$_pkgbasename
pkgver=2.11.0
pkgrel=2
_ubuntu_diff_version=${pkgver}-0ubuntu4
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=('x86_64')
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('lib32-expat' 'lib32-freetype2' 'lib32-bzip2' $_pkgbasename=$pkgver)
makedepends=(gcc-multilib)
provides=("${pkgname}-ubuntu")
conflicts=("${pkgname}-ubuntu")
replaces=("${pkgname}-ubuntu")
source=("http://www.fontconfig.org/release/$_pkgbasename-$pkgver.tar.bz2"
        "http://archive.ubuntu.com/ubuntu/pool/main/f/fontconfig/fontconfig_${_ubuntu_diff_version}.debian.tar.gz")
md5sums=('000bd4baf7aefa828e03414d0c8c7dc5'
         '50682ac34c77af146b97c2bcc01c3ffb')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  # apply ubuntu patches
  for _f in $(cat "$srcdir/debian/patches/series" | grep -v '#') ; do    
    patch -Np1 -i "$srcdir/debian/patches/$_f"
  done

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --with-templatedir=/etc/fonts/conf.avail \
              --with-xmldir=/etc/fonts \
              --localstatedir=/var \
              --disable-static \
              --with-default-fonts=/usr/share/fonts \
              --with-add-fonts=/usr/share/fonts \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir"/{etc,var}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
