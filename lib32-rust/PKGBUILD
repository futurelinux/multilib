_pkgbasename=rust
pkgname=lib32-$_pkgbasename
pkgver=1.36.0
pkgrel=1
pkgdesc='32-bit libraries for rust'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')
makedepends=('llvm' 'libffi' 'perl' 'python3' 'curl' 'cmake')
depends=('lib32-gcc-libs' "$_pkgbasename=$pkgver")
source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        config.toml
        librustc-llvm-libffi.patch)
sha256sums=('04c4e4d7213d036d6aaed392841496d272146312c0290f728b7400fccd15bb1b'
            'SKIP'
            'a4a4f8af81b92e70f308c55e19f29103ecd63b90577b067fd33530604f99c873'
            '49fa39e8775c9e3b3400d35d2df0728975371f9832d2d76d0dadf9fc73199774')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

prepare() {
  cd "rustc-$pkgver-src"
  cp "$srcdir"/config.toml config.toml
  patch -Np0 -i $srcdir/librustc-llvm-libffi.patch
}

build() {
  cd "rustc-$pkgver-src"
  python3 ./x.py build
}

package() {
  cd "rustc-$pkgver-src"

  target="$pkgdir"/"$(rustc --print sysroot)"/lib/rustlib
  install -d "$target"
  cp -r \
    build/x86_64-unknown-linux-gnu/stage2/lib/rustlib/i686-unknown-linux-gnu \
    "$target"

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}
