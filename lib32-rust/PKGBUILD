_pkgbasename=rust
pkgname=lib32-$_pkgbasename
pkgver=1.34.2
pkgrel=1
pkgdesc='32-bit libraries for rust'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')
makedepends=('llvm' 'libffi' 'perl' 'python2' 'curl' 'cmake')
depends=('lib32-gcc-libs' "$_pkgbasename=$pkgver")
source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        config.toml)
sha256sums=('c69a4a85a1c464368597df8878cb9e1121aae93e215616d45ad7d23af3052f56'
            'SKIP'
            '36c22ecdacbfeca9ab955726b4a701f687f8ed358a1b8ab2d7cc988408db44cb')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python2 ./x.py build
}

package() {
  cd "rustc-$pkgver-src"

  target="$pkgdir"/"$(rustc --print sysroot)"/lib/rustlib
  install -d "$target"
  cp -r \
    build/x86_64-unknown-linux-gnu/stage2/lib/rustlib/i686-unknown-linux-gnu \
    "$target"

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}
