# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=libsm
pkgname=lib32-$_pkgbasename
pkgver=1.2.2
pkgrel=1
pkgdesc="X11 Session Management library (32-bit)"
arch=(x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libice' 'lib32-util-linux' $_pkgbasename=$pkgver)
makedepends=('xorg-util-macros' 'xtrans' 'gcc-multilib')
options=('!libtool')
source=(${url}/releases/individual/lib/libSM-${pkgver}.tar.bz2)
md5sums=('499a7773c65aba513609fe651853c5f3')

build() {
  cd "${srcdir}/libSM-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libSM-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
