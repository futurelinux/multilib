_pkgbasename=libvorbis
pkgname=lib32-$_pkgbasename
pkgver=1.3.7
pkgrel=1
pkgdesc="Reference implementation of the Ogg Vorbis audio format (32-bit)"
arch=('x86_64')
license=(BSD)
url=https://www.xiph.org/vorbis/
depends=('lib32-libogg' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
source=(https://downloads.xiph.org/releases/vorbis/${_pkgbasename}-${pkgver}.tar.gz)
sha256sums=('0e982409a9c3fc82ee06e08205b1355e5c6aa4c36bca58146ef399621b0ce5ab')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
