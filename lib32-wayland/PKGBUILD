# Maintainer: Francesco Marinucci <franzmari [at] chakra-project [dot] it>

_pkgbasename=wayland
pkgname=lib32-$_pkgbasename
pkgver=1.15.0
pkgrel=1
pkgdesc='A computer display server protocol'
arch=('x86_64')
url='https://wayland.freedesktop.org'
license=('MIT')
depends=('lib32-glibc' 'lib32-libffi' 'lib32-expat' 'lib32-libxml2' $_pkgbasename=$pkgver )
source=("https://wayland.freedesktop.org/releases/wayland-${pkgver}.tar.xz"{,.sig})
validpgpkeys=('C7223EBE4EF66513B892598911A30156E0E67611') # Bryce Harrington
sha256sums=('eb3fbebb8559d56a80ad3753ec3db800f587329067962dbf65e14488b4b7aeb0'
            'SKIP')

build() {
  cd ${_pkgbasename}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-documentation \
    --disable-static
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbasename} "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
