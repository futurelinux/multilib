
_pkgbasename=libcroco
pkgname=lib32-$_pkgbasename
pkgver=0.6.12
pkgrel=1
pkgdesc='GNOME CSS2 parsing and manipulation toolkit (32-bit)'
url="https://git.gnome.org/browse/libcroco"
arch=('x86_64')
depends=('lib32-glib2' 'lib32-libxml2' "$_pkgbasename=$pkgver")
makedepends=('gcc-multilib' 'git' 'gtk-doc')
license=('LGPL')
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgbasename/0.6/$_pkgbasename-$pkgver.tar.xz")
sha256sums=('ddc4b5546c9fb4280a5017e2707fbd4839034ed1aba5b7d4372212f34f84f860')

prepare() {
  cd libcroco-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}
 
build() {
  cd libcroco-$pkgver

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static \
    --disable-gtk-doc
  make
}

check() {
  cd libcroco-$pkgver
  make check
}

package() {
  cd libcroco-$pkgver

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
