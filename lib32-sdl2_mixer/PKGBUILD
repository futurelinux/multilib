# maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=sdl2_mixer
pkgname=lib32-$_pkgbasename
pkgver=2.0.1
pkgrel=1
pkgdesc="A simple multi-channel audio mixer (Version 2, 32-bit)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('lib32-sdl2' 'lib32-libvorbis' 'lib32-libmodplug' 'lib32-smpeg2' 'lib32-flac' $_pkgbasename=$pkgver)
makedepends=('lib32-fluidsynth' 'gcc-multilib')
optdepends=('fluidsynth: MIDI software synth, replaces built-in timidity')
options=(!libtool)
source=("http://www.libsdl.org/projects/SDL_mixer/release/SDL2_mixer-${pkgver}.tar.gz")
sha256sums=('5a24f62a610249d744cbd8d28ee399d8905db7222bf3bdbc8a8b4a76e597695f')

build() {
  cd "${srcdir}/SDL2_mixer-${pkgver}/"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  ./configure --disable-static \
              --prefix=/usr \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/SDL2_mixer-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

