_pkgbasename=flac
pkgname=lib32-$_pkgbasename
pkgver=1.3.3
pkgrel=1
pkgdesc="Free Lossless Audio Codec (32-bit)"
arch=('x86_64')
url="https://xiph.org/flac/"
license=('BSD' 'GPL')
depends=('lib32-libogg' 'lib32-gcc-libs' $_pkgbasename=$pkgver)
makedepends=('nasm')
source=(https://downloads.xiph.org/releases/flac/${_pkgbasename}-${pkgver}.tar.xz)
sha256sums=('213e82bd716c9de6db2f98bcadbc4c24c7e2efe8c75939a1a84e28539c4e1748')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --disable-sse --libdir=/usr/lib32
  make
}

package() {
  cd "${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
