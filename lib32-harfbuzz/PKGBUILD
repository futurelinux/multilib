# Lib32 Packages for Chakra, part of chakra-project.org

_pkgbasename=harfbuzz
pkgname=lib32-$_pkgbasename
pkgver=1.4.7
pkgrel=1
pkgdesc="OpenType text shaping engine (32-bit)"
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(lib32-glib2 lib32-freetype2 lib32-icu $_pkgbasename gcc-multilib ragel git)
_commit=fc15e60eadfc89aca5b8815262b8ee888f285169  # tags/1.4.7^0
source=("git+https://anongit.freedesktop.org/git/harfbuzz#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgbasename
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgbasename
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename
 
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --without-cairo \
    --with-freetype \
    --with-glib \
    --with-gobject \
    --without-graphite2 \
    --with-icu \
    --disable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_lib32-harfbuzz() {
  depends=(lib32-glib2 lib32-freetype2 harfbuzz=$pkgver)

  cd $_pkgbasename
  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s harfbuzz "$pkgdir/usr/share/licenses/lib32-harfbuzz"

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/lib32/pkgconfig; cd ../hb-icu
  mv "$pkgdir"/usr/lib32/libharfbuzz-icu* ./usr/lib32
  mv "$pkgdir"/usr/lib32/pkgconfig/harfbuzz-icu.pc ./usr/lib32/pkgconfig
}

package_lib32-harfbuzz-icu() {
  pkgdesc="OpenType text shaping engine (32-bit, ICU integration)"
  depends=(lib32-harfbuzz lib32-icu harfbuzz-icu=$pkgver)

  mv hb-icu/* "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s harfbuzz-icu "$pkgdir/usr/share/licenses/lib32-harfbuzz-icu"
}
