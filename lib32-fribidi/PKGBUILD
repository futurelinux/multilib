
_pkgbasename=fribidi
pkgname=lib32-$_pkgbasename
pkgver=1.0.5
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (32-bit)"
arch=('x86_64')
license=('LGPL')
url="https://github.com/fribidi/fribidi/"
depends=('lib32-glibc' "$_pkgbasename=$pkgver")
makedepends=('git' 'meson')
_commit=5b6a16e8da12ae7ff482fbfa5a17b72bd518418f  # tags/v1.0.5^0
source=("git+https://github.com/fribidi/fribidi#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd fribidi
  git describe --tags | sed -e 's/-/+/g' -e 's/^v//'
}

prepare() {
  cd fribidi
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  chakra-meson fribidi build -D docs=false --libdir=/usr/lib32
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -rf "$pkgdir"/usr/{include,share,bin}
}
