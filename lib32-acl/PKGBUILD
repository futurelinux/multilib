# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=acl
pkgname=lib32-acl
pkgver=2.2.51
pkgrel=2
pkgdesc="Access control list libraries (32-bit)"
arch=('x86_64')
url="http://savannah.nongnu.org/projects/acl"
license=('LGPL')
depends=('lib32-attr' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://mirrors.zerg.biz/nongnu/acl/acl-${pkgver}.src.tar.gz)
md5sums=('3fc0ce99dc5253bdcce4c9cd437bc267')

build() {
  cd "${srcdir}/acl-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  export INSTALL_USER=root INSTALL_GROUP=root 
  ./configure --prefix=/usr --libdir=/usr/lib32 --libexecdir=/usr/lib32
  make 
}

package() {
  cd "${srcdir}/acl-${pkgver}"

  make DIST_ROOT="${pkgdir}" install install-lib install-dev

  rm -rf ${pkgdir}/usr/{bin,include,share}
}
