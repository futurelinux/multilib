_pkgbasename=libice
pkgname=lib32-$_pkgbasename
pkgver=1.0.10
pkgrel=1
pkgdesc="X11 Inter-Client Exchange library (32-bit)"
arch=(x86_64)
url="https://www.x.org/wiki/"
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('xtrans>=1.2.5' 'xorg-util-macros' 'lib32-gcc-libs')
source=(${url}/releases/individual/lib/libICE-${pkgver}.tar.bz2)
license=('custom')
sha1sums=('5b5eb125d4f43a3ab8153b0f850963ee6c982c24')

build() {
  cd "${srcdir}/libICE-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32

  make
}

package() {
  cd "${srcdir}/libICE-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
