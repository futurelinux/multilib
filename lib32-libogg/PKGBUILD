# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Francesco Marinucci <franzmari[at]chakra-project[dot]it>

_pkgbasename=libogg
pkgname=lib32-$_pkgbasename
pkgver=1.3.1
pkgrel=1
pkgdesc="Ogg bitstream and framing library (32-bit)"
arch=('x86_64')
url="http://www.xiph.org/ogg/"
license=('BSD')
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=(gcc-multilib)
options=('!libtool')
source=(http://downloads.xiph.org/releases/ogg/$_pkgbasename-$pkgver.tar.gz)
md5sums=('ba526cd8f4403a5d351a9efaa8608fbc')

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd $srcdir/${_pkgbasename}-${pkgver}
    ./configure --prefix=/usr --libdir=/usr/lib32
    make
}

package() {
    cd $srcdir/${_pkgbasename}-${pkgver}

    make DESTDIR=$pkgdir install

    rm -rf "$pkgdir"/usr/{share,bin}
    find "$pkgdir"/usr/include/ogg -type f -not -name config_types.h -delete
    mv "$pkgdir"/usr/include/ogg/config_types{,-32}.h
    mkdir -p "$pkgdir/usr/share/licenses"
    ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
