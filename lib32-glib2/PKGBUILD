_pkgbasename=glib2
pkgname=lib32-$_pkgbasename
pkgver=2.58.1
pkgrel=1
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (32-bit)"
url="http://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
depends=('lib32-libdbus' 'lib32-libffi' 'lib32-pcre' 'lib32-libelf' 'lib32-util-linux' "${_pkgbasename}=${pkgver}")
makedepends=('gcc-multilib' 'python3')
checkdepends=(desktop-file-utils lib32-glib2)
options=('!docs')
source=("http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-${pkgver}.tar.xz"
            gio-querymodules-32.hook)
sha256sums=('97d6a9d926b6aa3dfaadad3077cfb43eec74432ab455dff14250c769d526d7d6'
            '73b6791b58180e2a02fe908938e6c547ee1b263f8c387262f9a8c4105ffa999a')

prepare() {
  cd "${srcdir}/glib-${pkgver}"

}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  chakra-meson glib-$pkgver build \
    -D selinux=false \
    --libdir=/usr/lib32
  ninja -C build
}

check() {
  meson test -C build -t 2
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  rm -r "$pkgdir"/usr/{share,include}
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules -printf 'Removing %P\n' -delete
  mv "$pkgdir"/usr/bin/gio-querymodules{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}
