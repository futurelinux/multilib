_pkgbasename=libxi
pkgname=lib32-$_pkgbasename
pkgver=1.7.8
pkgrel=1
pkgdesc="X11 Input extension library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org"
depends=('lib32-libxext' 'lib32-libxfixes' 'inputproto' $_pkgbasename=$pkgver)
makedepends=('pkgconfig' 'xorg-util-macros' 'lib32-gcc-libs' 'automake')
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha256sums=('d8f2fa8d53141c41ff521627df9b2fa9c05f6f142fd9881152bab36549ac27bb')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXi-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --libdir=/usr/lib32

  make
}

package() {
  cd "${srcdir}/libXi-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
