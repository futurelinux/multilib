# maintainer: Neophytos Kolokotronis <tetris4 AT gmail DOT com>

_pkgbasename=pulseaudio
pkgname=lib32-libpulse
pkgdesc="A featureful, general-purpose sound server. (32-bit client libraries)"
pkgver=11.1
pkgrel=1
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=(LGPL)
depends=(lib32-libdbus lib32-systemd lib32-glib2
         lib32-libasyncns lib32-libcap lib32-libxtst lib32-libsm lib32-libsndfile
         lib32-libltdl
         $_pkgbasename)
makedepends=(gcc libtool intltool lib32-glib2 lib32-speexdsp git)
optdepends=('lib32-alsa-plugins: ALSA support')
options=(!emptydirs)
_commit=f93e372b0b2cb960df04de3f0a8fbea683bb8229  # tags/v11.1^0
source=("git+https://anongit.freedesktop.org/git/pulseaudio/pulseaudio#commit=$_commit")
sha256sums=('SKIP')

_pulsever=$pkgver
[[ $_pulsever = *.*.* ]] && _pulsever=${_pulsever%.*}

_libs="libpulse.la libpulse-simple.la libpulse-mainloop-glib.la"
_pkglibs="libpulsecommon-${_pulsever}.la libpulsedsp.la"

pkgver() {
  cd pulseaudio
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd pulseaudio
  NOCONFIGURE=1 ./bootstrap.sh
}

build() {
  cd pulseaudio

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib32 \
    --localstatedir=/var \
    --disable-hal \
    --disable-tcpwrap \
    --disable-bluez4 \
    --disable-samplerate \
    --disable-rpath \
    --disable-default-build-tests \
    DATADIRNAME=share

  # fight unused direct deps
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make -C src $_libs $_pkglibs
}

package() {
  cd pulseaudio
  make -j1 -C src DESTDIR="$pkgdir" \
    lib_LTLIBRARIES="$_libs" install-libLTLIBRARIES
  make -j1 -C src DESTDIR="$pkgdir" \
    pkglib_LTLIBRARIES="$_pkglibs" install-pkglibLTLIBRARIES
  make -j1 DESTDIR="$pkgdir" install-pkgconfigDATA
}
