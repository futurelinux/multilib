
_pkgbasename=sdl2_image
pkgname=lib32-$_pkgbasename
pkgver=2.0.5
pkgrel=1
pkgdesc="SDL2 image libraries (Version 2, 32-bit)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('lib32-sdl2' 'lib32-libpng' 'lib32-libtiff' 'lib32-libjpeg' 'lib32-libwebp' $_pkgbasename=$pkgver)
makedepends=('cmake' 'gcc')
source=("http://www.libsdl.org/projects/SDL_image/release/SDL2_image-${pkgver}.tar.gz")
sha256sums=('bdd5f6e026682f7d7e1be0b6051b209da2f402a2dd8bd1c4bd9c25ad263108d0')

build() {
  cd "${srcdir}/SDL2_image-${pkgver}/"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  ./configure --disable-static \
              --prefix=/usr \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/SDL2_image-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  
  # link license
  install -d "$pkgdir"/usr/share/licenses
  ln -s ${pkgname#*-} "$pkgdir"/usr/share/licenses/$pkgname
}

