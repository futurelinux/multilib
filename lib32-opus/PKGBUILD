# Contributions from Arch: https://projects.archlinux.org/svntogit/packages.git/tree/trunk?h=packages/opus

_pkgbase=opus
pkgname=lib32-$_pkgbase
pkgver=1.1.2
pkgrel=1
pkgdesc="codec designed for interactive speech and audio transmission over the Internet"
arch=('x86_64')
url="http://www.opus-codec.org/"
license=('BSD')
depends=('lib32-glibc' "${_pkgbase}=${pkgver}")
makedepends=('lib32-gcc-libs')
source=("http://downloads.xiph.org/releases/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
md5sums=('1f08a661bc72930187893a07f3741a91')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr \
              --enable-custom-modes \
              --disable-static  \
              --enable-intrinsics \
              --enable-rtcd \
              --libdir=/usr/lib32
  make
}

check() {
  cd ${_pkgbase}-${pkgver}
  make check
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
  install -d "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbase} "${pkgdir}"/usr/share/licenses/$pkgname
}
