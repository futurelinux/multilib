# Part of the X.org group

_pkgbasename=libxvmc
pkgname=lib32-$_pkgbasename
pkgver=1.0.10
pkgrel=1
pkgdesc="X11 Video Motion Compensation extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxv' $_pkgbasename=$pkgver)
makedepends=('pkgconfig' 'lib32-gcc-libs')
source=(${url}/releases/individual/lib/libXvMC-${pkgver}.tar.bz2)
sha1sums=('8c50ee4a43aff84d807da2122ec6b0d8e3ce4635')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/libXvMC-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/libXvMC-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"

}
