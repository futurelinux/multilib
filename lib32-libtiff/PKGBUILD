_pkgbasename=libtiff
pkgname=lib32-$_pkgbasename
pkgver=4.0.10
pkgrel=1
pkgdesc="Library for manipulation of TIFF images (32-bit)"
arch=('x86_64')
url="http://www.simplesystems.org/libtiff/"
license=('custom')
depends=('lib32-libjpeg-turbo' 'lib32-zlib' 'lib32-xz' $_pkgbasename=$pkgver)
makedepends=('lib32-libgl' 'lib32-libxmu' 'lib32-libxi' 'lib32-gcc-libs')
source=(https://download.osgeo.org/libtiff/tiff-${pkgver}.tar.gz)
sha1sums=('c783b80f05cdacf282aa022dc5f5b0ede5e021ae')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/tiff-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --with-docdir=/usr/share/doc/${pkgname} \
              --libdir=/usr/lib32 \
              --includedir=/usr/include/libtiff32
  make
}

package() {
  cd "${srcdir}/tiff-${pkgver}"
  make DESTDIR="${pkgdir}" install
  
  rm -rf "${pkgdir}"/usr/{share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
