#
# Chakra Packages for Chakra, part of chakra-project.org
#
# Maintainer: Michael Haesel <michael[dot]haesel[at]gmail[dot]com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Lauri Niskanen <ape@ape3000.com>

_pkgbasename=celt
pkgname=lib32-$_pkgbasename
pkgver=0.11.3
pkgrel=1
pkgdesc='Low-latency audio communication codec (32 bit)'
arch=('x86_64')
url='http://www.celt-codec.org'
license=('BSD')
depends=('lib32-glibc' "${_pkgbasename}=${pkgver}")
makedepends=('gcc-multilib')
source=("http://downloads.xiph.org/releases/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz")
sha256sums=('7e64815d4a8a009d0280ecd235ebd917da3abdcfd8f7d0812218c085f9480836')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make -C libcelt
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C libcelt DESTDIR="${pkgdir}" install

  # remove unneeded stuff
  rm -rf "${pkgdir}/usr/include"
}

# vim:set ts=2 sw=2 et:
