_pkgbasename=cracklib
pkgname=lib32-$_pkgbasename
pkgver=2.9.7
pkgrel=1
pkgdesc="Password Checking Library (32 bit)"
arch=('x86_64')
url="https://sourceforge.net/projects/cracklib"
license=('LGPL')
depends=('lib32-glibc' 'lib32-zlib' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
source=(https://github.com/cracklib/cracklib/releases/download/v${pkgver}/${_pkgbasename}-${pkgver}.tar.gz)
sha256sums=('8b6fd202f3f1d8fa395d3b7a5d821227cfd8bb4a9a584a7ae30cf62cea6287dd')

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure --prefix=/usr --libdir=/usr/lib32 --sbindir=/usr/bin --without-python
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}/usr"/{include,bin,share}
}
