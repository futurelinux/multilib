# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=zsnes
pkgver=1.51
pkgrel=5
pkgdesc="Super Nintendo emulator"
url="http://www.zsnes.com/"
arch=('x86_64')
license=('GPL')
depends=('gcc-libs-multilib' 'lib32-sdl' 'lib32-libpng>=1.5.0' 'lib32-libgl' 'lib32-ncurses' 'lib32-mesa')
makedepends=('nasm' 'gcc-multilib')

source=(http://downloads.sourceforge.net/${pkgname}/zsnes151src.tar.bz2
	zsnes.patch
	gcc47-fix.patch
	zsnes.desktop
	zsnes-1.51-libpng15.patch
	zsnes.png)
md5sums=('7071186bf80632ae88a153239498d8c9'
         '3af40c7d674ebfce1bb1ab6b7e9a8fe4'
         '951a54e05e4436fe1114308e4466ad74'
         'f28707c568cec5c9a4440ec70f2b123e'
         'b2df7ab745719e86c44f2e95b93874b9'
         'b5260c02adfc3b7ff725041214b00bfd')
         
prepare() {
  cd ${srcdir}/${pkgname}_1_51/
  patch -p1 -i ../zsnes.patch
  patch -p1 -i ../gcc47-fix.patch
  patch -p0 -i ../zsnes-1.51-libpng15.patch
}

build() {
  cd ${srcdir}/${pkgname}_1_51/src
  export CC="gcc -m32"
  export CXX="g++ -m32"

  ./autogen.sh --prefix=/usr \
    x_libraries=/usr/lib \
    force_arch=i686 \
    --enable-release

  make
}

package() {
  cd "$srcdir/${pkgname}_1_51/src"
  make DESTDIR=${pkgdir} install

  # adding --mandir= to autoconf line fails
  install -dm755 ${pkgdir}/usr/share

  install -Dm644 ${srcdir}/zsnes.desktop ${pkgdir}/usr/share/applications/zsnes.desktop
  install -Dm644 ${srcdir}/zsnes.png ${pkgdir}/usr/share/pixmaps/zsnes.png

  mv ${pkgdir}/usr/man ${pkgdir}/usr/share
}
