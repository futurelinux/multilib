_pkgbasename=util-linux
pkgname=lib32-$_pkgbasename
_pkgmajor=2.33
pkgver=${_pkgmajor}.2
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux (32-bit)"
url='https://github.com/karelzak/util-linux'
arch=('x86_64')
depends=('lib32-gcc-libs' $_pkgbasename=$pkgver)
provides=('libuuid.so' 'libblkid.so' 'libfdisk.so' 'libmount.so' 'libsmartcols.so')
makedepends=('lib32-gcc-libs')
license=('GPL2')
options=('!emptydirs')
source=("https://www.kernel.org/pub/linux/utils/util-linux/v$_pkgmajor/$_pkgbasename-$pkgver.tar.xz")
sha256sums=('631be8eac6cf6230ba478de211941d526808dba3cd436380793334496013ce97')

build() {
  cd "$_pkgbasename-$pkgver"

  ./configure \
      CC="${CC:-cc} -m32" \
      PKG_CONFIG_PATH="/usr/lib32/pkgconfig" \
      --libdir=/usr/lib32

  make lib{uuid,blkid,fdisk,mount,smartcols}.la
}

package() {
  make -C "$_pkgbasename-$pkgver" \
    DESTDIR="$pkgdir" \
    install-usrlib_execLTLIBRARIES \
    install-pkgconfigDATA
}
