# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=glew
pkgname=lib32-$_pkgbasename
pkgver=1.5.8
pkgrel=1
pkgdesc="A cross-platform C/C++ extension loading library (32 bit)"
arch=('x86_64')
url="http://glew.sourceforge.net"
license=('BSD' 'MIT' 'GPL')
depends=('lib32-libxmu' 'lib32-libxi' 'lib32-mesa' "$_pkgbasename>=$pkgver")
makedepends=('gcc-multilib')
source=(http://downloads.sourceforge.net/${_pkgbasename}/${_pkgbasename}-${pkgver}.tgz)
sha1sums=('450946935faa20ac4950cb42ff025be2c1f7c22e')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i 's|CC = cc|CC = gcc -m32|' config/Makefile.linux
  sed -i 's|LD = cc|LD = gcc -m32|' config/Makefile.linux
  sed -i 's|lib64|lib32|' config/Makefile.linux
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make GLEW_DEST="${pkgdir}/usr" install
  chmod 0755 "${pkgdir}/usr/lib32/libGLEW.so.${pkgver}"

  rm -rf "${pkgdir}"/usr/{include,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
