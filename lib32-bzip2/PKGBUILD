_pkgbasename=bzip2
pkgname=lib32-bzip2
pkgver=1.0.7
pkgrel=1
pkgdesc="A high-quality data compression program (32-bit)"
arch=('x86_64')
license=('custom')
url="https://sourceware.org/bzip2/"
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
source=(https://sourceware.org/pub/bzip2/${_pkgbasename}-$pkgver.tar.gz{,.sig})
sha256sums=('e768a87c5b1a79511499beb41500bcc4caf203726fff46a6f5f9ad27fe08ab2b'
            'SKIP')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mark@klomp.org>

build() {
  cd ${_pkgbasename}-${pkgver}

  sed -i "s|CC=gcc|CC=gcc -m32|" Makefile
  sed -i "s|CC=gcc|CC=gcc -m32|" Makefile-libbz2_so

  # add large-file support
  sed -e 's/^CFLAGS=\(.*\)$/CFLAGS=\1 \$(BIGFILES)/' -i ./Makefile-libbz2_so

  # use our optimization
  sed -i "s|-O2|${CFLAGS}|g" Makefile
  sed -i "s|-O2|${CFLAGS}|g" Makefile-libbz2_so

  make -f Makefile-libbz2_so
  make libbz2.a
}

package(){
  cd ${_pkgbasename}-${pkgver}
  install -Dm755 libbz2.so.1.0.7 "${pkgdir}"/usr/lib32/libbz2.so.1.0.7
  ln -s libbz2.so.1.0.7 "${pkgdir}"/usr/lib32/libbz2.so
  ln -s libbz2.so.1.0.7 "${pkgdir}"/usr/lib32/libbz2.so.1
  ln -s libbz2.so.1.0.7 "${pkgdir}"/usr/lib32/libbz2.so.1.0

  install -Dm644 libbz2.a ${pkgdir}/usr/lib32/libbz2.a
}
