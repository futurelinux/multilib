_pkgbasename=bzip2
pkgname=lib32-bzip2
pkgver=1.0.8
pkgrel=1
pkgdesc="A high-quality data compression program (32-bit)"
arch=('x86_64')
license=('custom')
url="https://sourceware.org/bzip2/"
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
source=(https://sourceware.org/pub/bzip2/${_pkgbasename}-$pkgver.tar.gz{,.sig})
sha256sums=('ab5a03176ee106d3f0fa90e381da478ddae405918153cca248e682cd0c4a2269'
            'SKIP')
validpgpkeys=('EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A') # Mark Wielaard <mark@klomp.org>

build() {
  cd ${_pkgbasename}-${pkgver}
 
  make -f Makefile-libbz2_so CC="gcc -m32 $CFLAGS $LDFLAGS"
  make libbz2.a CC="gcc -m32 $CFLAGS $LDFLAGS"
}

package(){
  cd ${_pkgbasename}-${pkgver}

  install -dm755 "$pkgdir"/usr/lib32
  cp -a libbz2.so* "$pkgdir"/usr/lib32
  ln -s libbz2.so.$pkgver "$pkgdir"/usr/lib32/libbz2.so

  install -Dm644 libbz2.a "$pkgdir"/usr/lib32/libbz2.a

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
}
