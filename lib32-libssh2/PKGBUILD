_pkgbasename=libssh2
pkgname=lib32-$_pkgbasename
pkgver=1.9.0
pkgrel=1
pkgdesc="A library implementing the SSH2 protocol as defined by Internet Drafts (32-bit)"
arch=('x86_64')
url="https://www.libssh2.org/"
license=('BSD')
depends=('lib32-openssl' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
source=("https://www.libssh2.org/download/$_pkgbasename-$pkgver.tar.gz")
md5sums=('1beefafe8963982adc84b408b2959927')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  ./configure \
      --prefix=/usr \
      --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{share,bin,sbin,include}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
