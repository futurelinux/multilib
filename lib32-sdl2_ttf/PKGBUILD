# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer: Giuseppe Cal√† <jiveaxe@gmail.com>

_pkgbasename=sdl2_ttf
pkgname=lib32-$_pkgbasename
pkgver=2.0.14
pkgrel=1
pkgdesc="A library that allows you to use TrueType fonts in your SDL applications (Version 2, 32-bit)"
arch=('x86_64')
url="http://www.libsdl.org"
license=('MIT')
depends=('lib32-sdl2' 'lib32-freetype2' $_pkgbasename=$pkgver)
makedepends=('cmake' 'gcc-multilib')
options=(!libtool)
source=("http://www.libsdl.org/projects/SDL_ttf/release/SDL2_ttf-${pkgver}.tar.gz")
sha256sums=('34db5e20bcf64e7071fe9ae25acaa7d72bdc4f11ab3ce59acc768ab62fe39276')

build() {
  cd "${srcdir}/SDL2_ttf-${pkgver}/"
  
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  ./autogen.sh
  ./configure --disable-static --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/SDL2_ttf-${pkgver}/"
  make DESTDIR="${pkgdir}/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

