_pkgbasename=sqlite
pkgname=lib32-sqlite
_srcver=3260000
_docver=${_srcver}
pkgver=3.26.0
pkgrel=1
pkgdesc="A C library that implements an SQL database engine (32-bit)"
arch=('x86_64')
license=('custom')
url="http://www.sqlite.org/"
depends=(lib32-glibc $_pkgbasename=$pkgver 'lib32-gcc-libs')
makedepends=('tcl' 'gcc' 'lib32-readline')
source=(https://www.sqlite.org/2018/sqlite-src-${_srcver}.zip)
sha1sums=('a05429d6a8337d60ddc7c6381b49941059a55f68')
options=('!makeflags')
replaces=("lib32-sqlite3<$pkgver")
conflicts=("lib32-sqlite3<$pkgver")

build() {
  cd "$srcdir"/sqlite-src-$_srcver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  export LTLINK_EXTRAS="-ldl"
  export CFLAGS="$CFLAGS -DSQLITE_ENABLE_FTS3=1 \
                         -DSQLITE_ENABLE_COLUMN_METADATA=1 \
                         -DSQLITE_ENABLE_UNLOCK_NOTIFY \
                         -DSQLITE_ENABLE_DBSTAT_VTAB=1 \
                         -DSQLITE_ENABLE_FTS3_TOKENIZER=1 \
                         -DSQLITE_ENABLE_RTREE=1 \
                         -DSQLITE_SECURE_DELETE"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-tcl \
    --disable-static

  make
}

package() {
  cd "$srcdir"/sqlite-src-$_srcver

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
