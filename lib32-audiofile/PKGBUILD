_pkgbasename=audiofile
pkgname=lib32-$_pkgbasename
pkgver=0.3.6
pkgrel=3
pkgdesc="Silicon Graphics Audio File Library (32-bit)"
arch=('x86_64')
url="http://www.68k.org/~michael/audiofile/"
license=('LGPL')
depends=('lib32-glibc' "$_pkgbasename=$pkgver" 'lib32-alsa-lib' 'lib32-flac')
makedepends=(gcc-multilib)
options=('!libtool')
source=("http://audiofile.68k.org/$_pkgbasename-$pkgver.tar.gz"
        01_gcc6.patch
        03_CVE-2015-7747.patch
        04_clamp-index-values-to-fix-index-overflow-in-IMA.cpp.patch
        05_Always-check-the-number-of-coefficients.patch
        06_Check-for-multiplication-overflow-in-MSADPCM-decodeSam.patch
        07_Check-for-multiplication-overflow-in-sfconvert.patch
        08_Fix-signature-of-multiplyCheckOverflow.-It-returns-a-b.patch
        09_Actually-fail-when-error-occurs-in-parseFormat.patch
        10_Check-for-division-by-zero-in-BlockCodec-runPull.patch)
md5sums=('2731d79bec0acef3d30d2fc86b0b72fd'
         '1267e0afdfad10e4a81b9e8d9cd2d32b'
         'a23f3e37b222bca7d4e1a30e30322df3'
         '50eaec6ed8d61c64d6ece3c23bc80e48'
         '9f31116e18b2174104cd0dca831bb45d'
         'fb6d6f139b9fda594cb0fc114c9ce468'
         '07366b67a7ec81ae827547a930e571f9'
         'f5ea93bbd07ce7d70217b2513c18cf14'
         '75a6e6ff0389f69f42e723f43281d1d8'
         '5f9cb76d1c99f3799c77dfcd915c8ada')

prepare() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  
  patch -Np1 -i ../01_gcc6.patch
  patch -Np1 -i ../03_CVE-2015-7747.patch
  patch -Np1 -i ../04_clamp-index-values-to-fix-index-overflow-in-IMA.cpp.patch
  patch -Np1 -i ../05_Always-check-the-number-of-coefficients.patch
  patch -Np1 -i ../06_Check-for-multiplication-overflow-in-MSADPCM-decodeSam.patch
  patch -Np1 -i ../07_Check-for-multiplication-overflow-in-sfconvert.patch
  patch -Np1 -i ../08_Fix-signature-of-multiplyCheckOverflow.-It-returns-a-b.patch
  patch -Np1 -i ../09_Actually-fail-when-error-occurs-in-parseFormat.patch
  patch -Np1 -i ../10_Check-for-division-by-zero-in-BlockCodec-runPull.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-docs --disable-examples
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
