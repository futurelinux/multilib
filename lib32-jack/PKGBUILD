_pkgbasename=jack
pkgname=lib32-$_pkgbasename
pkgver=0.125.0
pkgrel=3
pkgdesc="A low-latency audio server (32-bit)"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=("$_pkgbasename=$pkgver" 'lib32-db' 'lib32-glibc')
makedepends=('gcc-multilib' 'git' 'lib32-libsamplerate' 'lib32-readline')
url="http://jackaudio.org/"
source=("$pkgname::git+https://github.com/jackaudio/${_pkgbasename}1.git#tag=${pkgver}"
        "git+https://github.com/jackaudio/example-clients"
        "git+https://github.com/jackaudio/headers"
        "git+https://github.com/jackaudio/tools")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  git submodule init
  git config submodule.example-clients.url "${srcdir}/example-clients"
  git config submodule.jack.url "${srcdir}/headers"
  git config submodule.tools.url "${srcdir}/tools"
  git submodule update
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make -C libjack
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C libjack DESTDIR="${pkgdir}" install
}

