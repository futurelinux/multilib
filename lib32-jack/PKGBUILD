_pkgbasename=jack
pkgname=lib32-$_pkgbasename
_longname=jack-audio-connection-kit
pkgver=0.125.0
pkgrel=1
pkgdesc="A low-latency audio server (32-bit)"
arch=('x86_64')
license=('GPL' 'LGPL')
depends=("$_pkgbasename=$pkgver" 'lib32-db' 'lib32-libsamplerate' 'lib32-readline')
makedepends=('lib32-gcc-libs')
url="http://jackaudio.org/"
source=("http://jackaudio.org/downloads/${_longname}-${pkgver}.tar.gz")
md5sums=('d9b7e230aeae2d5b45c7a822b2d2dd15')

build() {
  cd "${srcdir}/${_longname}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make -C libjack
}

package() {
  cd ${srcdir}/${_longname}-${pkgver}

  make DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C libjack DESTDIR="${pkgdir}" install
}

