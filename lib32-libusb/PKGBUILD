# Contributions from Arch:
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: 3V0LU710N <db_eee-at-hotmail-dot-com>

pkgname=lib32-libusb
pkgver=1.0.23
pkgrel=1
pkgdesc="A cross-platform user library to access USB devices (32-bit)"
arch=('x86_64')
url="https://libusb.info/"
license=('LGPL')
depends=('lib32-glibc' 'libusb' 'lib32-systemd')
replaces=('lib32-libusb1' 'lib32-libusbx')
provides=("lib32-libusbx=$pkgver")
makedepends=('lib32-gcc-libs')
source=(https://github.com/libusb/libusb/releases/download/v$pkgver/libusb-$pkgver.tar.bz2)
sha256sums=('db11c06e958a82dac52cf3c65cb4dd2c3f339c8a988665110e0d24d19312ad8d')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "libusb-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make -j1
}

package () {
  cd "${srcdir}/libusb-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include
}
