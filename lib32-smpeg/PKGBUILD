# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=smpeg
pkgname=lib32-${_pkgbasename}
pkgver=0.4.4
pkgrel=3
pkgdesc="SDL MPEG Player Library (32-bit)"
arch=('x86_64')
url="http://icculus.org/smpeg/"
license=('LGPL')
depends=('lib32-sdl' $_pkgbasename=$pkgver)
options=('!libtool' '!makeflags')
source=(http://mirrors.dotsrc.org/lokigames/open-source/smpeg/${_pkgbasename}-${pkgver}.tar.gz smpeg-0.4.4-gcc41.patch)
md5sums=('59c76ac704088ef5539210190c4e1fe3'
		 '8b979a58307d7196655758bd3d2466c4')

build() {
  export CC='gcc -m32'
  export CXX="g++ -m32"
  export LIBS=-L/usr/lib32
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  cd ${srcdir}/${_pkgbasename}-${pkgver}
  patch -p1 < ../smpeg-0.4.4-gcc41.patch

  ./configure --prefix=/usr --libdir=/usr/lib32
  make LDFLAGS+=-lstdc++
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install

  #fix aclocal warnings
  sed -i "s#(AM_PATH_SMPEG#([AM_PATH_SMPEG]#" ${pkgdir}/usr/share/aclocal/smpeg.m4

  rm -rf "${pkgdir}"/usr/{bin,include,share,man,info}
}


