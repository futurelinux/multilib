_pkgbasename=libxrender
pkgname=lib32-$_pkgbasename
pkgver=0.9.10
pkgrel=1
pkgdesc="X Rendering Extension client library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11' $_pkgbasename=$pkgver)
makedepends=('pkgconfig' 'lib32-gcc-libs' renderproto)
source=(${url}/releases/individual/lib/libXrender-${pkgver}.tar.bz2)
md5sums=('802179a76bded0b658f4e9ec5e1830a4')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXrender-${pkgver}"
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXrender-${pkgver}"

  make DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
