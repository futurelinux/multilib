# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=alsa-lib
pkgname=lib32-${_pkgbasename}
pkgver=1.1.0
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="http://www.alsa-project.org"
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('gcc-multilib')
license=('GPL')
source=(ftp://ftp.alsa-project.org/pub/lib/${_pkgbasename}-$pkgver.tar.bz2)
md5sums=('69515ca73c3c4a212ef45160dea846c1')

build() {
  cd $srcdir/${_pkgbasename}-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

package() {
  cd $srcdir/${_pkgbasename}-$pkgver

  make DESTDIR=$pkgdir install

  # Clean up lib32 package
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
