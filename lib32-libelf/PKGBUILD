# Arch contributor: https://git.archlinux.org/svntogit/community.git/plain/trunk/PKGBUILD?h=packages/lib32-libelf

_pkgbasename=elfutils
pkgname=lib32-libelf
pkgver=0.176
pkgrel=1
pkgdesc="Libraries to handle ELF object files and DWARF debugging information (32-bit)"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL' 'GPL3')
depends=('lib32-bzip2' 'lib32-zlib' "libelf=$pkgver")
makedepends=('lib32-gcc-libs')
provides=('lib32-elfutils')
conflicts=('lib32-elfutils')
replaces=('lib32-elfutils')
source=(https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2)
options=('staticlibs')
sha256sums=('eb5747c371b0af0f71e86215a5ebb88728533c3a104a43d4231963f308cd1023')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
	
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
