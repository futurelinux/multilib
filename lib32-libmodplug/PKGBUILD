_pkgbasename=libmodplug
pkgname=lib32-${_pkgbasename}
pkgver=0.8.9.0
pkgrel=1
pkgdesc="A MOD playing library (32 bit)"
arch=('x86_64')
url="http://modplug-xmms.sourceforge.net/"
license=('custom')
depends=('lib32-gcc-libs' 'lib32-glibc' $_pkgbasename=$pkgver)
source=("https://downloads.sourceforge.net/modplug-xmms/${_pkgbasename}-${pkgver}.tar.gz")
sha256sums=('457ca5a6c179656d66c01505c0d95fafaead4329b9dbaa0f997d00a3508ad9de')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  sed 's|includedir=${prefix}/include|includedir=${prefix}/include/libmodplug/|' -i "${pkgdir}"/usr/lib32/pkgconfig/libmodplug.pc
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libmodplug "${pkgdir}"/usr/share/licenses/lib32-libmodplug
}
