# Part of the X.org group
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# contributor (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxinerama
pkgname=lib32-$_pkgbasename
pkgver=1.1.3
pkgrel=1
pkgdesc="X11 Xinerama extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxext' 'xineramaproto' $_pkgbasename=$pkgver)
makedepends=('xorg-util-macros' gcc-multilib)
options=(!libtool)
source=(${url}/releases/individual/lib/libXinerama-${pkgver}.tar.bz2)
sha1sums=('6a3956d73f7457ef0b4db12806d99c6918e328e3')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXinerama-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXinerama-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
