# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# contributor: (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=alsa-plugins
pkgname=lib32-$_pkgbasename
pkgver=1.1.6
pkgrel=1
pkgdesc="Extra alsa plugins (32-bit)"
arch=(x86_64)
url="http://www.alsa-project.org"
license=(GPL)
depends=(lib32-alsa-lib $_pkgbasename=$pkgver)
makedepends=(lib32-libpulse lib32-jack lib32-speex lib32-libsamplerate lib32-gcc-libs)
optdepends=('lib32-libpulse: PulseAudio plugin'
            'lib32-jack: Jack plugin'
            'lib32-libsamplerate: libsamplerate resampling plugin'
            'lib32-speex: libspeexdsp resampling plugin')
source=("ftp://ftp.alsa-project.org/pub/plugins/$_pkgbasename-$pkgver.tar.bz2")
sha1sums=('a3601513f1a54eaef606744c9c9f4e9d4d44bf40')

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-avcodec
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir/usr/share"
}
