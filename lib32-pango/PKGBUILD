# Lib32 Packages for Chakra, part of chakra-project.org

_pkgbasename=pango
pkgname=lib32-$_pkgbasename
pkgver=1.42.4
pkgrel=1
pkgdesc="A library for layout and rendering of text (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=(lib32-libthai lib32-cairo lib32-libxft lib32-harfbuzz lib32-fribidi "$_pkgbasename=$pkgver")
makedepends=(gcc-multilib gobject-introspection git meson)
_commit=a6a89818246b0b14c0fe3d00d6120929fc0a11d9  # tags/1.42.4^0
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pango
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pango
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  chakra-meson pango build --libdir=/usr/lib32
  ninja -C build
}


package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -r "$pkgdir"/usr/{bin,lib,share,include}
}
