_pkgbasename=orc
pkgname=lib32-${_pkgbasename}
pkgver=0.4.28
pkgrel=1
pkgdesc="The Oild Runtime Compiler. Multilib"
arch=('x86_64')
license=('custom')
url='http://code.entropywave.com/projects/orc/'
depends=('lib32-glibc' $_pkgbasename=$pkgver)
makedepends=('valgrind' 'lib32-gcc-libs')
source=("http://gstreamer.freedesktop.org/data/src/orc/orc-${pkgver}.tar.xz")
md5sums=('6b582ec4b3275c5efd51e3ae6406d445')

build() {
  cd orc-${pkgver}

  # multilib
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir/" install

  # Removing unneeded files
  rm -rf ${pkgdir}/usr/{bin,include,share}

  # license
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
