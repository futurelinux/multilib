# Part of the X.org group

_pkgbasename=libxrandr
pkgname=lib32-$_pkgbasename
pkgver=1.5.1
pkgrel=1
pkgdesc="X11 RandR extension library (32-bit)"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext' 'lib32-libxrender' 'randrproto' $_pkgbasename=$pkgver)
makedepends=('xorg-util-macros' 'lib32-gcc-libs')
source=(${url}/releases/individual/lib/libXrandr-${pkgver}.tar.bz2)
sha1sums=('7232fe2648b96fed531208c3ad2ba0be61990041')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXrandr-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXrandr-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
