# Part of the X.org group

_pkgbasename=libxcursor
pkgname=lib32-$_pkgbasename
pkgver=1.1.15
pkgrel=1
pkgdesc="X cursor management library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxfixes' 'lib32-libxrender' $_pkgbasename=$pkgver)
makedepends=('xorg-util-macros' 'lib32-gcc-libs')
options=('!libtool')
source=(${url}/releases/individual/lib/libXcursor-${pkgver}.tar.bz2)
sha256sums=('294e670dd37cd23995e69aae626629d4a2dfe5708851bbc13d032401b7a3df6b')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXcursor-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXcursor-${pkgver}"

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
