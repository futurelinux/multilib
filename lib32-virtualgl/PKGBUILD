_pkgbasename=virtualgl
pkgname=lib32-${_pkgbasename}
pkgver=2.6.1
pkgrel=1
pkgdesc="32-bit serverside components for 64-bit VirtualGL servers."
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('virtualgl' 'lib32-libxv' 'lib32-glu' 'lib32-libjpeg-turbo')
makedepends=('lib32-libxv' 'lib32-glu' 'cmake' 'lib32-gcc-libs' 'lib32-mesa' 'lib32-libxtst')
conflicts=('virtualgl32-bin')
source=("https://downloads.sourceforge.net/project/virtualgl/${pkgver}/VirtualGL-${pkgver}.tar.gz")
sha1sums=('0b5db8cd90e658605a7e04bc694a7739ec3edce4')

prepare() {
  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"

  mkdir "$srcdir/VirtualGL-$pkgver"/build
}

build() {
  cd "$srcdir/VirtualGL-$pkgver"/build

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib32/libturbojpeg.so \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
      -DCMAKE_INSTALL_BINDIR=/usr/bin \
      -DX11_X11_LIB=/usr/lib32/libX11.so \
      -DX11_Xext_LIB=/usr/lib32/libXext.so \
      -DOPENGL_gl_LIBRARY=/usr/lib32/libGL.so
  make
}

package() {
  cd "$srcdir/VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir/usr"
  mv bin/glxspheres glxspheres32
  rm -rf bin
  mkdir -p bin
  mv glxspheres32 bin/

  rm -rf "$pkgdir/usr/share"
  rm -rf "$pkgdir/usr/include"
}
