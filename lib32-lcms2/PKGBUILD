
_pkgbasename=lcms2
pkgname=lib32-$_pkgbasename
pkgver=2.9
pkgrel=2
pkgdesc="Small-footprint color management engine, version 2 (32-bit)"
arch=('x86_64')
license=('MIT')
depends=('lib32-libtiff' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
url="http://www.littlecms.com"
source=(http://downloads.sourceforge.net/sourceforge/lcms/${_pkgbasename}-${pkgver}.tar.gz)
sha1sums=('60bea9875e017dd1c466e988c2ad98f8766e4e55')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd ${_pkgbasename}-${pkgver}

  make check
}

package() {
  cd ${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "${pkgdir}/usr/share/licenses"
  ln -s ${_pkgbasename} "${pkgdir}/usr/share/licenses/${pkgname}"
}
