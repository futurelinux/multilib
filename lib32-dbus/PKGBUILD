pkgname=lib32-dbus
_pkgbasename=dbus
pkgver=1.12.12
pkgrel=2
pkgdesc="DBus library (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/Software/dbus"
license=('GPL' 'custom')
depends=($_pkgbasename=$pkgver)
makedepends=(lib32-gcc-libs lib32-glib2 lib32-expat git autoconf-archive docbook-xsl)
provides=(lib32-libdbus)
conflicts=(lib32-libdbus)
replaces=(lib32-libdbus)
_commit=d4f8423bbff9b3c5fca2d8009c28d1cff4652788  # tags/dbus-1.12.12^0
source=("git+https://gitlab.freedesktop.org/dbus/dbus.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd dbus
  git describe --tags | sed 's/^dbus-//;s/-/+/g'
}

prepare() {
  cd dbus
  NOCONFIGURE=1 ./autogen.sh
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

    cd dbus

    ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib32 \
    --libexecdir=/usr/lib/dbus-1.0 \
    runstatedir=/run \
    --with-dbus-user=dbus \
    --with-system-pid-file=/run/dbus/pid \
    --with-system-socket=/run/dbus/system_bus_socket \
    --with-console-auth-dir=/run/console/ \
    --enable-inotify \
    --disable-static \
    --disable-verbose-mode \
    --disable-asserts \
    --disable-checks \
    --disable-systemd \
    --disable-tests \
    --without-x
    make
}

package() {
  make DESTDIR="$pkgdir" -C dbus install

  rm -r "$pkgdir"/usr/{bin,include,lib,share}
  rm -r "$pkgdir"/{etc,var}

  install -d "$pkgdir/usr/share/licenses"
  ln -s dbus "$pkgdir/usr/share/licenses/$pkgname"
}
