# Lib32 Packages for Chakra, part of chakra-project.org
#
# maintainer (x86_64): Giuseppe Cal√† <jiveaxe@gmail.com>
# maintainer (x86_64): Anke Boersma <abveritas[at]chakra-project[dot]org>

_pkgbasename=libxss
pkgname=lib32-$_pkgbasename
pkgver=1.2.1
pkgrel=2
pkgdesc="X11 Screen Saver extension library (32-bit)"
arch=(x86_64)
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext' 'scrnsaverproto' $_pkgbasename=$pkgver)
makedepends=('xorg-util-macros' gcc-multilib)
options=('!libtool')
source=(${url}/releases/individual/lib/libXScrnSaver-${pkgver}.tar.bz2)
sha1sums=('3c29e2222fd68e26e408234c51c6d8b2edf6ab19')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXScrnSaver-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc \
    --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXScrnSaver-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
