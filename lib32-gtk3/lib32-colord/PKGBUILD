# Lib32 Packages for Chakra, part of chakra-project.org
#
# Maintainer:  AlmAck <almack@chakra-project.org>
# Contributor: jtts

_pkgbasename=colord
pkgname=lib32-$_pkgbasename
pkgver=1.2.9
pkgrel=1
pkgdesc="System daemon for managing color devices (32-bit)"
arch=(x86_64)
url="http://www.freedesktop.org/software/colord"
license=(GPL2)
depends=('lib32-lcms2' 'lib32-libgusb' 'lib32-polkit' 'lib32-sqlite3' 'lib32-systemd' 'lib32-dbus' 'lib32-glib2' $_pkgbasename=$pkgver) #shared-color-profiles 
makedepends=('gcc-multilib' 'intltool' 'gobject-introspection' 'vala') # docbook2x sane bash-completion
source=($url/releases/$_pkgbasename-$pkgver.tar.xz)
sha1sums=('1b6319812b12ab7f906983959bbaaf2e5b78bdb7')
    

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $_pkgbasename-$pkgver
  
  # put udev files in /usr/lib
  sed -i "/slashlibdir=/s#/lib#/usr/lib#" configure
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib32/$_pkgbasename --libdir=/usr/lib32 --disable-static \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --enable-vala --disable-volume-search \
    --disable-nls --disable-bash-completion \
    --with-daemon-user=colord
    #--enable-sane 
    # Not recognized by configure
    #--disable-manpages
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/{etc,lib,var,usr/{bin,include,lib,share}}
}
