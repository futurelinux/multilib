# Maintainer: Francesco Marinucci <franzmari [at] chakra-project [dot] it>

_pkgbasename=libxkbcommon
pkgname=lib32-$_pkgbasename
pkgver=0.5.0
pkgrel=1
pkgdesc="Keyboard handling library using XKB data"
arch=('x86_64')
url="http://xkbcommon.org/"
license=('custom')
depends=(xkeyboard-config 'lib32-glibc' $_pkgbasename=$pkgver )
makedepends=('lib32-libxcb' 'xorg-util-macros')
source=("http://xkbcommon.org/download/${pkgname#*-}-${pkgver}.tar.xz")
sha256sums=('90bd7824742b9a6f52a6cf80e2cadd6f5349cf600a358d08260772615b89d19c')

build() {
  cd ${_pkgbasename}-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix='/usr' --libdir='/usr/lib32' --disable-{docs,static}
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/include

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbasename} "${pkgdir}"/usr/share/licenses/${pkgname}
}
