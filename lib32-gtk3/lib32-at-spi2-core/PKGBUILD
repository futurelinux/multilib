# Lib32 Packages for Chakra, part of chakra-project.org
#
# Maintainer:  AlmAck <almack@chakra-project.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=at-spi2-core
pkgname=lib32-$_pkgbasename
pkgver=2.26.0
pkgrel=1
pkgdesc="Protocol definitions and daemon for D-Bus at-spi (32-bit)"
arch=(x86_64)
url="http://www.gnome.org"
license=(GPL2)
depends=('lib32-dbus' 'lib32-glib2' 'lib32-libxtst'  $_pkgbasename=$pkgver)
makedepends=('gcc-multilib' 'intltool' 'libtool' 'gobject-introspection')
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgbasename/${pkgver%.*}/$_pkgbasename-$pkgver.tar.xz)
md5sums=('ef3de25da46da8f650915205eb7e1a33')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd $_pkgbasename-$pkgver

  autoreconf --force --install

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/at-spi2-core' \
    --sysconfdir='/etc' \
    --disable-xevie
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  # cleanup for lib32 package
  rm -rf "${pkgdir}"/{etc,bin,sbin,usr/{bin,lib,include,share},var}
}
