_pkgbasename=libxtst
pkgname=lib32-$_pkgbasename
pkgver=1.2.3
pkgrel=1
pkgdesc="X11 Testing -- Resource extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxext' 'lib32-libxi' 'recordproto' $_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs' 'xorg-util-macros')
options=('!libtool')
source=(${url}/releases/individual/lib/libXtst-${pkgver}.tar.bz2)
md5sums=('ef8c2c1d16a00bd95b9fdcef63b8a2ca')

build() {
  cd "${srcdir}/libXtst-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --disable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXtst-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
