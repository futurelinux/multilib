# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.23.20
pkgrel=1
pkgdesc="Provides a way to load and enumerate PKCS#11 modules (32-bit)"
arch=(x86_64)
url="https://p11-glue.freedesktop.org"
license=(BSD)
depends=(lib32-glibc lib32-libtasn1 lib32-libffi $_pkgbasename)
makedepends=(git meson lib32-systemd)
source=("https://github.com/p11-glue/p11-kit/releases/download/$pkgver/$_pkgbasename-$pkgver.tar.xz"{,.sig})
sha256sums=('14d86024c3dfd6b967d9bc0b4ec7b2973014fe7423481f4d230a1a63b8aa6104'
            'SKIP')
validpgpkeys=('462225C3B46F34879FC8496CD605848ED7E69871')

prepare() {
  cd ${_pkgbasename}-${pkgver}
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  chakra-meson ${_pkgbasename}-${pkgver} build \
    --buildtype debugoptimized \
    --libdir /usr/lib32 \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source

  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  rm -rf "${pkgdir}"/etc "${pkgdir}"/usr/{bin,include,lib,share}

  install -d "$pkgdir/usr/share/licenses"
  ln -s ${_pkgbasename} "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
