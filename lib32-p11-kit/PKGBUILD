# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

_pkgbasename=p11-kit
pkgname=lib32-$_pkgbasename
pkgver=0.23.20
pkgrel=1
pkgdesc="Provides a way to load and enumerate PKCS#11 modules (32-bit)"
arch=(x86_64)
url="https://p11-glue.freedesktop.org"
license=(BSD)
depends=(lib32-glibc lib32-libtasn1 lib32-libffi $_pkgbasename)
makedepends=(git meson lib32-systemd)
source=("git+https://github.com/p11-glue/p11-kit#tag=$pkgver")
sha256sums=('SKIP')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF')

prepare() {
  cd ${_pkgbasename}
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="i686-pc-linux-gnu-pkg-config"

  chakra-meson p11-kit build \
    --buildtype debugoptimized \
    --libdir /usr/lib32 \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source

  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  rm -rf "${pkgdir}"/etc "${pkgdir}"/usr/{bin,include,lib,share}

  install -d "$pkgdir/usr/share/licenses"
  ln -s ${_pkgbasename} "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
