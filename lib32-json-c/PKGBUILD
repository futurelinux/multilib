
_pkgbasename=json-c
pkgname=lib32-$_pkgbasename
pkgver=0.12
pkgrel=1
pkgdesc="A JSON implementation in C (32-bit)"
arch=('x86_64')
url="https://github.com/json-c/json-c/wiki"
depends=($_pkgbasename=$pkgver)
makedepends=('lib32-gcc-libs')
license=('GPL')
options=(!libtool !makeflags)
source=("https://s3.amazonaws.com/${_pkgbasename}_releases/releases/${_pkgbasename}-${pkgver}.tar.gz")
md5sums=('3ca4bbb881dfc4017e8021b5e0a8c491')


build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -Wno-error=unused-but-set-variable"

  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-static
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir/usr/include"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
