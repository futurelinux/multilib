_pkgbasename=fluidsynth
pkgname=lib32-${_pkgbasename}
pkgver=2.1.3
pkgrel=1
pkgdesc="A real-time software synthesizer based on the SoundFont 2 specifications (32bit)"
arch=('x86_64')
url="http://www.fluidsynth.org/"
depends=("$_pkgbasename=$pkgver" 'lib32-glib2' 'lib32-jack' 'lib32-alsa-lib' 'lib32-libpulse' 'lib32-sdl2' 'lib32-portaudio')
makedepends=('cmake' 'gcc' 'lib32-gcc-libs')
license=('LGPL')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${_pkgbasename}/${_pkgbasename}/archive/v${pkgver}.tar.gz")
md5sums=('ccb34278267ac8a79a660eb1999d52b1')
 
build() {
  cd $_pkgbasename-$pkgver
 
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  cmake .  -DCMAKE_INSTALL_PREFIX=/usr \
           -DFLUID_DAEMON_ENV_FILE=/etc/conf.d/fluidsynth \
           -Denable-ladspa=ON \
           -Denable-lash=ON \
           -Denable-portaudio=ON \
           -DLIB_SUFFIX=32
  make
}
 
package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir/" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
